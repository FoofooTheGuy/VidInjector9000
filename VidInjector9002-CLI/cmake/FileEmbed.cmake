#[[
from https://gitlab.com/jhamberg/cmake-examples
MIT License

Copyright (c) 2901 Jonathan Hamberg

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
]]

function(FileEmbedSetup)
	if (NOT EXISTS ${CMAKE_BINARY_DIR}/file_embed)
		file(MAKE_DIRECTORY ${CMAKE_BINARY_DIR}/file_embed)
	endif ()
	
	if (NOT EXISTS ${CMAKE_BINARY_DIR}/file_embed/placeholder.c)
		file(WRITE ${CMAKE_BINARY_DIR}/file_embed/placeholder.c "")
	endif ()
	
	add_library(file_embed ${CMAKE_BINARY_DIR}/file_embed/placeholder.c)
	target_include_directories(file_embed PUBLIC ${CMAKE_BINARY_DIR}/file_embed)
endfunction()

function(FileEmbedAddArray file)
	FileEmbedGenerateArray(${file} var)
	FileEmbedArray(${file} ${var})
endfunction()

function(FileEmbedAddVector file)
	FileEmbedGenerateVector(${file} var)
	FileEmbedVector(${file} ${var})
endfunction()

function(FileEmbedArray file var)
	target_sources(file_embed PUBLIC ${var})
	
	add_custom_command(
		OUTPUT ${var}
		COMMAND ${CMAKE_COMMAND}
		-DRUN_FILE_EMBED_GENERATE_ARRAY=1
		-DFILE_EMBED_GENERATE_PATH_ARRAY=${file}
		-P ${CMAKE_SOURCE_DIR}/cmake/FileEmbed.cmake
		MAIN_DEPENDENCY ${file}
	)
endfunction()

function(FileEmbedVector file var)
	target_sources(file_embed PUBLIC ${var})
	
	add_custom_command(
		OUTPUT ${var}
		COMMAND ${CMAKE_COMMAND}
		-DRUN_FILE_EMBED_GENERATE_VECTOR=1
		-DFILE_EMBED_GENERATE_PATH_VECTOR=${file}
		-P ${CMAKE_SOURCE_DIR}/cmake/FileEmbed.cmake
		MAIN_DEPENDENCY ${file}
	)
endfunction()

function(FileEmbedGenerateArray file generated_cpp)
	get_filename_component(base_filename ${file} NAME)
	set(output_filename "${base_filename}.cpp")
	string(MAKE_C_IDENTIFIER ${base_filename} c_name)
	file(READ ${file} content HEX)
	#message(${content})

	# Separate into individual bytes.
	string(REGEX MATCHALL "([A-Fa-f0-9][A-Fa-f0-9])" SEPARATED_HEX ${content})
	
	set(outfile_cpp ${CMAKE_BINARY_DIR}/file_embed/${c_name}.cpp)
	set(outfile_hpp ${CMAKE_BINARY_DIR}/file_embed/${c_name}.hpp)
	
	file(SIZE ${file} filesize)
	file(WRITE ${outfile_cpp}
		"// This code was generated by CMake script.\n"
		"//\n"
		"// Changes to this file may cause incorrect behavior and will be lost if the code is regenerated.\n"
		"#include \"${c_name}.hpp\"\n"
		"\n"
		"const uint8_t ${c_name}_data[${filesize}] = {\n"
		"	"
	)
	
	writefile(${outfile_cpp} ${c_name} size)
	
	file(APPEND ${outfile_cpp}
		"\n"
		"};\n"
	)
	
	message("Generating ${c_name}.hpp")
	file(WRITE ${outfile_hpp}
		"// This code was generated by CMake script.\n"
		"//\n"
		"// Changes to this file may cause incorrect behavior and will be lost if the code is regenerated.\n"
		"#ifndef ${c_name}_H\n"
		"#define ${c_name}_H\n"
		"\n"
		"#include <stdint.h>\n"
		"\n"
		"extern const uint8_t ${c_name}_data[${size}];\n"
		"\n"
		"#endif // ${c_name}_H\n"
	)
	
	set(${generated_cpp} ${outfile_cpp} PARENT_SCOPE)
endfunction()

function(FileEmbedGenerateVector file generated_cpp)
	get_filename_component(base_filename ${file} NAME)
	set(output_filename "${base_filename}.cpp")
	string(MAKE_C_IDENTIFIER ${base_filename} c_name)
	file(READ ${file} content HEX)
	#message(${content})
	
	# Separate into individual bytes.
	string(REGEX MATCHALL "([A-Fa-f0-9][A-Fa-f0-9])" SEPARATED_HEX ${content})
	
	set(outfile_cpp ${CMAKE_BINARY_DIR}/file_embed/${c_name}.cpp)
	set(outfile_hpp ${CMAKE_BINARY_DIR}/file_embed/${c_name}.hpp)
	
	file(WRITE ${outfile_cpp}
		"// This code was generated by CMake script.\n"
		"//\n"
		"// Changes to this file may cause incorrect behavior and will be lost if the code is regenerated.\n"
		"#include \"${c_name}.hpp\"\n"
		"\n"
		"std::vector<uint8_t> ${c_name}_data = {\n"
		"	"
	)
	
	writefile(${outfile_cpp} ${c_name} size)
	
	file(APPEND ${outfile_cpp}
		"\n"
		"};\n"
	)
	
	message("Generating ${c_name}.hpp")
	file(WRITE ${outfile_hpp}
		"// This code was generated by CMake script.\n"
		"//\n"
		"// Changes to this file may cause incorrect behavior and will be lost if the code is regenerated.\n"
		"#ifndef ${c_name}_H\n"
		"#define ${c_name}_H\n"
		"\n"
		"#include <vector>\n"
		"\n"
		"#include <stdint.h>\n"
		"\n"
		"extern std::vector<uint8_t> ${c_name}_data;\n"
		"\n"
		"#endif // ${c_name}_H\n"
	)
	
	set(${generated_cpp} ${outfile_cpp} PARENT_SCOPE)
endfunction()

function(writefile output_file c_name size)
	set(output_cpp "")
	file(SIZE ${file} filesize)
	set(counter 0)
	set(size 0)
	set(oldpercent 0)
	foreach(hex IN LISTS SEPARATED_HEX)
		string(APPEND output_cpp "0x${hex}, ")
		math(EXPR counter "${counter}+1")
		math(EXPR size "${size}+1")
		# 12 bytes per line
		if(counter GREATER 11)
			string(APPEND output_cpp "\n	")
			set(counter 0)
		endif()
		file(APPEND ${outfile_cpp}
			${output_cpp}
		)
		set(output_cpp "")
		# here https://stackoverflow.com/a/20790402
		math(EXPR percent "((100 * ${size} + (${size} + ${filesize})/2) / (${size} + ${filesize})) * 2")
		if(percent GREATER oldpercent)
			pad_string(padpercent ${percent} 3)
			message("[${padpercent}%] Generating ${c_name}.cpp")
			set(oldpercent ${percent})
		endif()
	endforeach()
	set(size ${size} PARENT_SCOPE)
endfunction()

# https://gist.github.com/jtanx/96ded5e050d5ee5b19804195ee5cf5f9
function(pad_string output str length)
	string(LENGTH "${str}" _strlen)
	math(EXPR _strlen "${length} - ${_strlen}")
	
	string(REPEAT " " ${_strlen} _pad)
	set(padded "${_pad}${str}")
	
	set(${output} "${padded}" PARENT_SCOPE)
endfunction()

if (RUN_FILE_EMBED_GENERATE_ARRAY)
	FileEmbedGenerateArray(${FILE_EMBED_GENERATE_PATH_ARRAY} var)
endif ()

if (RUN_FILE_EMBED_GENERATE_VECTOR)
	FileEmbedGenerateVector(${FILE_EMBED_GENERATE_PATH_VECTOR} var)
endif ()
